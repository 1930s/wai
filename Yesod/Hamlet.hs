{-# LANGUAGE TypeSynonymInstances #-}
{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# OPTIONS_GHC -fno-warn-orphans #-}
module Yesod.Hamlet
    ( -- * Hamlet library
      Hamlet
    , hamlet
    , HtmlContent (..)
    , htmlContentToByteString
      -- * Convert to something displayable
    , hamletToContent
    , hamletToRepHtml
      -- * Page templates
    , PageContent (..)
    )
    where

import Text.Hamlet
import Text.Hamlet.Monad ( outputHtml, hamletToByteString
                         , htmlContentToByteString)
import Yesod.Content
import Yesod.Handler
import Data.Convertible.Text
import Web.Routes.Quasi (Routes)

-- | Content for a web page. By providing this datatype, we can easily create
-- generic site templates, which would have the type signature:
--
-- > PageContent url -> Hamlet url
data PageContent url = PageContent
    { pageTitle :: HtmlContent
    , pageHead :: Hamlet url
    , pageBody :: Hamlet url
    }

-- | Converts the given Hamlet template into 'Content', which can be used in a
-- Yesod 'Response'.
hamletToContent :: Hamlet (Routes master) -> GHandler sub master Content
hamletToContent h = do
    render <- getUrlRender
    return $ toContent $ hamletToByteString render h

-- | Wraps the 'Content' generated by 'hamletToContent' in a 'RepHtml'.
hamletToRepHtml :: Hamlet (Routes master) -> GHandler sub master RepHtml
hamletToRepHtml = fmap RepHtml . hamletToContent

instance ConvertSuccess String (Hamlet url) where
    convertSuccess = outputHtml . Unencoded . cs
instance ConvertSuccess String HtmlContent where
    convertSuccess = Unencoded . cs
